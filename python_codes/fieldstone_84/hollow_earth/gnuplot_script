set term pdf enhanced font "Times,11pt"
set grid

set style rect fc lt -1 fs solid 0.15 noborder
set obj rect from 3480e3, graph 0 to 6371000, graph 1 

rho=4000
Ggrav=6.6738480e-11
Rin=3480e3
Rout=6371e3
F=-2.*pi*rho*Ggrav*Rout**2

set key outside

set ylabel 'U'
set output 'gravpot.pdf'
plot[][-5e7:-2e7]\
'line_pointmass.ascii'   u 1:2 w l lw 1.2 t 'point mass',\
'line_prism.ascii'       u 1:2 w l lw 1.3 t 'prism',\
2*pi*Ggrav*rho*(Rin**2-Rout**2) lt -1 lw .5 dashtype 2 notitle,\
4.*pi/3.*Ggrav*rho*(x**2/2.+Rin**3/x)+F lt -1 lw .5 dashtype 2 t 'analytical',\
-4*pi/3*Ggrav*rho*(Rout**3-Rin**3)/x lt -1  lw .5 dashtype 2 notitle

#'line_quadrature.ascii'  u 1:2 w l t 'quadrature',\

set ylabel '|g|'
set output 'gravnorm.pdf'
plot[][-5:20]\
'line_pointmass.ascii'   u 1:6  w l lw 1.2 t 'point mass',\
'line_prism.ascii'       u 1:6  w l lw 1.2 t 'prism',\
4.*pi/3.*Ggrav*rho*(x-Rin**3/x**2) lt -1 lw .5 dashtype 2 t 'analytical',\
4*pi/3*Ggrav*rho*(Rout**3-Rin**3)/x**2 lt -1 lw .5 dashtype 2 notitle


#'line_quadrature.ascii'  u 1:6 w l t 'quadrature',\
