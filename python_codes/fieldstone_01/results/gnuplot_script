set term pdf enhanced font "Times,12pt"
set grid
set xlabel 'x'
set ylabel 'pressure'

set output 'pressure.pdf'
plot[0:1][]\
'pressure.ascii' u 1:3 w p t '32x32',\
x*(1-x)-1./6. lt -1 t 'analytical '

#################################################

set output 'errors.pdf'
set key outside
set log xy
set xlabel 'h'
set ylabel 'error'
set format y "10^{%L}"
plot[][]\
'errors' u (1./sqrt($3)):6 w lp ps .5 t 'velocity',\
'errors' u (1./sqrt($3)):9 w lp ps .5 t 'pressure',\
x**2/10. w l lt -1 dashtype 2 t 'x^2',\
x**1/10. w l lt -1 dashtype 3 t 'x^1'

#################################################
unset log xy
set xlabel 'y'
set ylabel 'velocity'

set output 'vel_profile.pdf'
plot[][]\
'profiles32.ascii' u 1:2 w l lt 1 dt 3 t 'u, 32^2',\
'profiles64.ascii' u 1:2 w l lt 1 dt 2 t 'u, 64^2',\
'profiles96.ascii' u 1:2 w l lt 1 dt 1 t 'u, 96^2',\
'profiles32.ascii' u 1:3 w l lt 2 dt 3 t 'v, 32^2',\
'profiles64.ascii' u 1:3 w l lt 2 dt 2 t 'v, 64^2',\
'profiles96.ascii' u 1:3 w l lt 2 dt 1 t 'v, 96^2',\
'profiles32.ascii' u 1:4 w l lt 3 dt 3 t '|v|, 32^2',\
'profiles64.ascii' u 1:4 w l lt 3 dt 2 t '|v|, 64^2',\
'profiles96.ascii' u 1:4 w l lt 3 dt 1 t '|v|, 96^2',\
'depth_average.txt' u 2:3 w l lt 4  t 'ASPECT',\
2*x*(1-3*x+2*x**2)/30 lt -1 dt 2 t 'analytical' 
